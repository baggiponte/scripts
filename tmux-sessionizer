#! /usr/bin/env zsh

tmux-sessionizer() {
    requires zoxide sk tmux

    local ignores=(
        ".DS_Store"
        ".Rproj.user"
        ".git"
        ".idea"
        ".ipynb_checkpoints"
        ".mypy_cache"
        ".pytest_cache"
        ".ruff_cache"
        ".terraform"
        ".venv"
        ".vscode"
        "__pycache__"
        "assets"
        "debug/"
        "node_modules/"
        "raycast"
        "renv/"
        "target/"
        "venv"
    )

    local sk_args=(
        --ansi
        --height=40%
        --preview="eza --color --all --group-directories-first --icons --level=2 --tree --ignore-glob='${(j:|:)ignores}' -- {}"
        --preview-window=down,40%
        --cmd="zoxide query --list"
    )

    local dir dirname

    dir="$(sk "${sk_args[@]}")"
    dirname="$(basename "${dir/./}")"

    if ! tmux has-session -t "$dirname" 2>/dev/null ; then
        tmux new-session -d -s "$dirname" -c "$dir"
    fi

    if [[ ! -z "$TMUX" ]]; then
        tmux switch-client -t "$dirname"
    fi

    tmux attach -t "$dirname"
}


tmux-sessionizer
